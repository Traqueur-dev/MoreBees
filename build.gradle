plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.0.0-beta10"
}

group = 'fr.traqueur'

project.ext {
    targetFolder = file("target")
    apiFolder = file("target-api")
    libraries = []
}

allprojects { subproject ->
    version = rootProject.property('version')

    apply from: rootProject.file('gradle/add-library-dependency.gradle')
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven { url = "https://jitpack.io" }
        maven { url = "https://mvn.lumine.io/repository/maven-public/" }
    }

    dependencies {
        implementation("com.github.Traqueur-dev.CommandsAPI:platform-spigot:4.2.1")
        implementation("com.github.Traqueur-dev.CommandsAPI:core:4.2.1")
        implementation("com.github.Traqueur-dev:RecipesAPI:2.0.3")
        implementation 'com.github.Traqueur-dev:Structura:1.0.0'
    }

    def targetJavaVersion = 21
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'

        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    build {
        dependsOn shadowJar
    }

    shadowJar {
        archiveBaseName.set(rootProject.name)
        archiveVersion.set(rootProject.version)
        archiveClassifier.set('')
        relocate "fr.traqueur.commands", "fr.traqueur.morebees.api.commands"
        relocate "fr.traqueur.recipes", "fr.traqueur.morebees.api.recipes"
        relocate "fr.traqueur.structura", "fr.traqueur.morebees.api.structura"
    }
}

repositories {
    maven { url = "https://maven.devs.beer/" }
    maven {url = "https://repo.oraxen.com/releases" }
    maven {url = "https://repo.nexomc.com/releases" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    implementation(project(":api"))

    compileOnly("com.ticxo.modelengine:ModelEngine:R4.0.8")
    compileOnly("dev.lone:api-itemsadder:4.0.10")
    compileOnly("io.th0rgal:oraxen:1.191.0")
    compileOnly("com.nexomc:nexo:1.9.0")
}

shadowJar {
    mergeServiceFiles()
    destinationDirectory.set(rootProject.targetFolder)
}

tasks {
    runServer {
        minecraftVersion("1.21.4")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.register("cleanTarget") {
    doLast {
        delete rootProject.targetFolder
        delete rootProject.apiFolder
    }
}

tasks.named("clean") {
    dependsOn "cleanTarget"
}

processResources {
    filteringCharset = 'UTF-8'

    doFirst {
        filesMatching('plugin.yml') {
            expand(
                    name: rootProject.name,
                    version: version,
                    libraries: "\n" + (rootProject.ext.libraries ?: []).collect { "  - \"$it\"" }.join("\n")
            )
        }
    }
}
